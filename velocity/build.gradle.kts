plugins {
    alias(libs.plugins.runvelocity)
    alias(libs.plugins.blossom)
    alias(libs.plugins.shadow)
}

dependencies {
    compileOnly(libs.velocity)
    annotationProcessor(libs.velocity)
    implementation(libs.bstats)
}

tasks {
    build {
        dependsOn(shadowJar)
    }
    clean {
        // Deletes the directory that is generated by the runVelocity plugin
        delete("run")
    }
    runVelocity {
        velocityVersion(libs.versions.velocity.get())
    }
    shadowJar {
        archiveBaseName.set("${rootProject.name}-Proxy")
        archiveClassifier.set("")
        relocate("org.bstats", "io.github._4drian3d.signedvelocity.libs.bstats")
        minimize()
        doLast {
            copy {
                from(archiveFile)
                into("${rootProject.projectDir}/build")
            }
        }
    }
}

blossom {
    replaceTokenIn("src/main/java/io/github/_4drian3d/signedvelocity/velocity/Constants.java")
    replaceToken("{version}", project.version)
}
