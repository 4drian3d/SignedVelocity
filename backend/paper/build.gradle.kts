plugins {
    id("io.papermc.paperweight.userdev") version "2.0.0-beta.19"
    alias(libs.plugins.runpaper)
    alias(libs.plugins.pluginyml.paper)
    alias(libs.plugins.shadow)
}

dependencies {
    paperweight.paperDevBundle("1.21.8-R0.1-SNAPSHOT")
    implementation(projects.signedvelocityBackendCommon)
    implementation(projects.signedvelocityShared)
}

tasks {
    clean {
        // Deletes the directory that is generated by the runPaper plugin
        delete("run")
    }
    build {
        dependsOn(shadowJar)
    }
    shadowJar {
        archiveBaseName.set("${rootProject.name}-Paper")
        archiveClassifier.set("")
        doLast {
            copy {
                from(archiveFile)
                into("${rootProject.projectDir}/build")
            }
        }
    }
    runServer {
        minecraftVersion("1.21.8")
        jvmArgs("-Dcom.mojang.eula.agree=true")
    }
}

paper {
    name = "SignedVelocity"
    main = "io.github._4drian3d.signedvelocity.paper.SignedVelocity"
    authors = listOf("4drian3d")

    foliaSupported = true
    apiVersion = "1.21"
    website = "https://modrinth.com/plugin/signedvelocity"
}
